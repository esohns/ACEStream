#version 130

// shader_6_common.glsl
#define size iResolution.xy
#define pixel(a, p) texture(a, p/vec2(textureSize(a,0)))
#define texel(a, p) texelFetch(a, ivec2(p), 0)
#define ch0 iChannel0
#define ch1 iChannel1
#define ch2 iChannel2
#define ch3 iChannel3
#define PI 3.14159265

#define dt 0.4
#define prop 0.5

ivec2 N;
int tot_n;

float hash11(float p)
{
    p = fract(p * 15.1031);
    p *= p + 33.33;
    p *= p + p;
    return fract(p) - 0.5;
}

float hash12(vec2 p)
{
  vec3 p3  = fract(vec3(p.xyx) * .1031);
    p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.x + p3.y) * p3.z);
}


vec2 hash21(float p)
{
  vec3 p3 = fract(vec3(p) * vec3(.1031, .1030, .0973));
  p3 += dot(p3, p3.yzx + 33.33);
    return fract((p3.xx+p3.yz)*p3.zy);

}

vec2 hash22(vec2 p)
{
  vec3 p3 = fract(vec3(p.xyx) * vec3(.1031, .1030, .0973));
    p3 += dot(p3, p3.yzx+33.33);
    return fract((p3.xx+p3.yz)*p3.zy);
}

const int k = 1664525;  

ivec4 hash( ivec4 x )
{
    x = ((x>>8)^x.wxyz)*k;
    x = ((x>>8)^x.wxyz)*k;
    x = ((x>>8)^x.wxyz)*k;
    x = ((x>>8)^x.wxyz)*k;
    return ivec4(x);
}

ivec2 i2xy(int id)
{
    return ivec2(id%N.x, id/N.x);
}

int xy2i(ivec2 p)
{
    return p.x + p.y*N.x;
}

ivec2 cross_distribution(int i)
{
    return (1<<(i/4)) * ivec2( ((i&2)/2)^1, (i&2)/2 ) * ( 2*(i%2) - 1 );
}

float sdSegment( in vec2 p, in vec2 a, in vec2 b )
{
    vec2 pa = p-a, ba = b-a;
    float h = clamp( dot(pa,ba)/dot(ba,ba), 0.0, 1.0 );
    return length( pa - ba*h );
}
// shader_6_common.glsl

uniform vec2 iResolution;
uniform int iFrame;
uniform sampler2D iChannel0;
uniform sampler2D iChannel1;

ivec4
get (int id)
{
  return ivec4 (floor (texel (ch0, i2xy (id))));
}

vec4
save (ivec4 v)
{
  return vec4 (v);
}

ivec4 u;
vec4 d;
vec2 pos;
int tid;

vec4
getParticle (int id)
{
  return texel (ch1, i2xy (id));
}

void
sort (int utemp)
{
  if (utemp == tid || utemp < 0)
    return;

  vec4 part = getParticle (utemp);
  vec2 dx = part.xy - pos;
  float dtemp = length (dx);

  if (dx.x > abs (dx.y))
  {
    if (d.x > dtemp) 
    {
      d.x = dtemp;
      u.x = utemp;
    }
  }
  else if (dx.x < -abs (dx.y))
  {
    if (d.y > dtemp) 
    {
      d.y = dtemp;
      u.y = utemp;
    }
  }
  else if (dx.y > abs (dx.x))
  {
    if (d.z > dtemp) 
    {
      d.z = dtemp;
      u.z = utemp;
    }
  }
  else if (d.w > dtemp)
  {
    d.w = dtemp;
    u.w = utemp;
  }
}

void
sortneighbor (int id)
{
  ivec4 nb = get (id);
  for (int j = 0; j < 4; j++)
    sort (nb[j]);
}

void
main ()
{
  ivec2 p = ivec2 (gl_FragCoord.xy);
  N = ivec2 (prop * iResolution.xy);
  tot_n = N.x * N.y;
  if (p.x > N.x || p.y > N.y)
    discard;

  int id = xy2i (p);

  u = ivec4 (-1); d = vec4 (1e10); 

  tid = id;
  pos = getParticle (id).xy;

  sortneighbor (id);

  for (int i = 0; i < 8; i++)
    sort (hash (ivec4 (p, iFrame, i)).x % tot_n);

  ivec4 nb = get (id);
  for (int i = 0; i < 4; i++)
    sortneighbor (nb[i]); 

  if (any (lessThan (u, ivec4 (-1))) || any (greaterThan (u, ivec4 (tot_n))))
    u = ivec4 (0);

  gl_FragColor = save (u);
}
